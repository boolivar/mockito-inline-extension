plugins {
    id "java-library"
    id "jacoco"
    id "io.github.boolivar.jdoctest.jdoc-spock" version "0.11.0"
    id "io.github.boolivar.sonatype-portal-publish" version "0.1.0"
    id "org.sonarqube" version "6.3.1.5724"
    id "pl.allegro.tech.build.axion-release" version "1.20.1"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

scmVersion {
    tag.prefix = ""
    useHighestVersion = true
    versionIncrementer("incrementMinorIfNotOnRelease", [releaseBranchPattern: "(?>release|(?>hot)?fix)/.+"])
    branchVersionCreator = [
        "master": "simple",
        "release/.*": "simple",
        ".*": "versionWithBranch"
    ]
}

group = "io.github.boolivar"
version = scmVersion.version
description = "JUnit Extension to simplify Mockito static and constructor mocking"

sonar {
    properties {
        property "sonar.projectKey", "boolivar_mockito-inline-extension"
        property "sonar.organization", "boolivar"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

sonatypePublish {
    autoPublish = mavenCentralAutoPublish.toBoolean()
}

tasks.named("compileJava") {
    options.release = 8
}

tasks.named("test") {
    useJUnitPlatform()
    finalizedBy "jacocoTestReport"
}

tasks.named("jacocoTestReport") {
    reports {
        xml.required = true
    }
}

tasks.named("check") {
    dependsOn "jdocSpockTest"
}

tasks.named("jar") {
    dependsOn "check"
}

tasks.named("javadoc") {
    options.addBooleanOption("Xdoclint:none", true)
}

repositories {
    mavenCentral()
}

configurations {
    internalPlatform {
        visible = false
    }
    [compileClasspath, runtimeClasspath, testCompileClasspath, testRuntimeClasspath].each {
        it.extendsFrom internalPlatform
    }
}

dependencies {
    internalPlatform platform("org.junit:junit-bom:5.13.4")

    api "org.junit.jupiter:junit-jupiter-api"
    api "org.mockito:mockito-core:5.18.0"

    testImplementation "org.apache.groovy:groovy:4.0.27"
    testImplementation "org.assertj:assertj-core:3.27.4"
    testImplementation "org.junit.jupiter:junit-jupiter"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }
            afterEvaluate {
                pom {
                    name = "$groupId:$artifactId"
                    description = project.description
                    url = "https://github.com/boolivar/mockito-inline-extension"
                    inceptionYear = "2024"
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://raw.githubusercontent.com/boolivar/mockito-inline-extension/$version/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "boolivar"
                            name = "Aleksey Krichevskiy"
                            email = "boolivar@gmail.com"
                            organizationUrl = "https://github.com/boolivar"
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/boolivar/mockito-inline-extension.git"
                        developerConnection = "scm:git:ssh://github.com:boolivar/mockito-inline-extension.git"
                        url = "https://github.com/boolivar/mockito-inline-extension"
                    }
                }
            }
        }
    }
}
