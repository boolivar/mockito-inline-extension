plugins {
    id "java-library"
    id "jacoco"
    id "io.github.boolivar.jdoctest.jdoc-spock" version "0.10.1"
    id "org.sonarqube" version "6.0.0.5145"
    id "pl.allegro.tech.build.axion-release" version "1.18.16"
}

group = "io.github.boolivar"
version = scmVersion.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

scmVersion {
    tag.prefix = ""
    useHighestVersion = true
    versionIncrementer("incrementMinorIfNotOnRelease", [releaseBranchPattern: "(?>release|(?>hot)?fix)/.+"])
    branchVersionCreator = [
        "master": "simple",
        "release/.*": "simple",
        ".*": "versionWithBranch"
    ]
}

sonar {
    properties {
        property "sonar.projectKey", "boolivar_mockito-inline-extension"
        property "sonar.organization", "boolivar"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

tasks.named("compileJava") {
    options.release = 8
}

tasks.named("test") {
    useJUnitPlatform()
    finalizedBy "jacocoTestReport"
}

tasks.named("jacocoTestReport") {
    reports {
        xml.required = true
    }
}

tasks.named("check") {
    dependsOn "jdocSpockTest"
}

tasks.named("jar") {
    dependsOn "check"
}

tasks.named("javadoc") {
    options.addBooleanOption("Xdoclint:none", true)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.junit:junit-bom:5.11.3")
    implementation "org.junit.jupiter:junit-jupiter-api"
    implementation "org.mockito:mockito-core:5.14.2"

    testImplementation "org.apache.groovy:groovy:4.0.24"
    testImplementation "org.assertj:assertj-core:3.26.3"
    testImplementation "org.junit.jupiter:junit-jupiter"
}